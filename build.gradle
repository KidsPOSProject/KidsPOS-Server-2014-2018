group 'info.nukoneko'
version '0.0.1'

apply plugin: 'war'
apply plugin: 'jetty'

def javaVersion = 1.8
def defaultEncoding = 'UTF-8'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
}

def JerseyVersion = '2.22.1'
def JettyVersion = '8.1.18.v20150929'
def JacksonVersion = '2.7.0'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile 'javax:javaee-api:7.0'

    compile "org.glassfish.jersey.core:jersey-server:$JerseyVersion"
    compile "org.glassfish.jersey.containers:jersey-container-servlet-core:$JerseyVersion"

    providedCompile "org.eclipse.jetty:jetty-server:$JettyVersion"
    providedCompile "org.eclipse.jetty:jetty-webapp:$JettyVersion"
    providedCompile "org.eclipse.jetty:jetty-jsp-2.1:+"

    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$JacksonVersion"

    compile 'org.xerial:sqlite-jdbc:3.7.2'
}

compileJava {
    options.encoding = defaultEncoding
}

jettyRun {
    httpPort = 80
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

war {
    // 1
    from {
        configurations.providedCompile.collect {
            it.isDirectory() ? it : project.zipTree(it)
        }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    // 2
    from fileTree(dir: 'build/classes/main', include: '**/Launcher.class')

    // 3
    manifest.attributes('Main-Class': 'info.nukoneko.kidspos.Launcher')
}